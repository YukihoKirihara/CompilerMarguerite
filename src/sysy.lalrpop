use crate::ast::*;

grammar;

/// Lexical Analysis
match {
    /// Ignore space and comment.
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]*" => {},
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
    /// Default
    _
}

/// Syntax Analysis
/// Compilation unit
pub CompUnit: CompUnit = <global_items: (GlobalItem)*> => CompUnit{ <> };

/// Global Item
pub GlobalItem: GlobalItem = {
    Decl => GlobalItem::Decl(<>),
    FuncDef => GlobalItem::FuncDef(<>),
}

/// Declaration of variables
Decl: Decl = {
    ConstDecl => Decl::ConstDecl(<>),
    VarDecl => Decl::VarDecl(<>),
}

/// Declaration of constant variables, with multiple case supported.
ConstDecl: ConstDecl = {
    "const" "int" <const_def0: ConstDef> <mut const_defs: ("," <ConstDef>)*> ";" => {
        const_defs.insert(0, const_def0);
        ConstDecl{const_defs}
    }
}

/// Definition of constant variables, where the initial value is a must.
ConstDef: ConstDef = {
    <ident: Ident> <dims: ("[" <ConstExp> "]")*> "=" <const_init_val: ConstInitVal> => {
        ConstDef{ <> }
    }
}

/// Initial values of constant variables
ConstInitVal: ConstInitVal = {
    <const_exp: ConstExp> => ConstInitVal::ConstExp( <> ),
    "{" "}" => ConstInitVal::ConstArrayInitVal(Vec::new()),
    "{" <const_init_val0: ConstInitVal> <mut const_init_vals: ("," <ConstInitVal>)*> "}" => {
        const_init_vals.insert(0, const_init_val0);
        ConstInitVal::ConstArrayInitVal(const_init_vals)
    },
}

/// Declaration of (mutable) variables, with multiple case supported.
VarDecl: VarDecl = {
    "int" <var_def0: VarDef> <mut var_defs: ("," <VarDef>)*> ";" => {
        var_defs.insert(0, var_def0);
        VarDecl{var_defs}
    }
}

/// Definition of (mutable) variables, where the initial value is optional.
VarDef: VarDef = {
    <ident: Ident> <dims: ("[" <ConstExp> "]")*> <init_val: ("=" <InitVal>)?> => {
        VarDef{ <> }
    }
}

/// Initial values of (mutable) variables.
InitVal: InitVal = {
    <exp: Exp> => InitVal::Exp(exp),
    "{" "}" => InitVal::ArrayInitVal(Vec::new()),
    "{" <init_val0: InitVal> <mut init_vals: ("," <InitVal>)*> "}" => {
        init_vals.insert(0, init_val0);
        InitVal::ArrayInitVal(init_vals)
    }
}

/// Definition of functions
FuncDef: FuncDef = {
    <func_prefix: FuncPrefix> ")" <block: Block> => {
        FuncDef {func_type: func_prefix.0, func_fparams: Vec::new(), ident: func_prefix.1, block: block}
    },
    <func_prefix: FuncPrefix> <func_fparam0: FuncFParam> <mut func_fparams: ("," <FuncFParam>)*> ")" <block: Block> => {
        func_fparams.insert(0, func_fparam0);
        FuncDef {func_type: func_prefix.0, func_fparams: func_fparams, ident: func_prefix.1, block: block}
    }
}

/// Prefix of Definition of functions, in avoidance of conflict.
/// Function types of functions
FuncPrefix: (FuncType, String) = {
    "void" <ident: Ident> "(" => (FuncType::Void, <>),
    "int" <ident: Ident> "(" => (FuncType::Int, <>),
}

/// Formal parameter of functions
FuncFParam: FuncFParam = {
    "int" <ident: Ident> => FuncFParam{ident: ident, sub_dims: None},
    "int" <ident: Ident> "[" "]" <sub_dims: ("[" <ConstExp> "]")*> => FuncFParam{ident: ident, sub_dims: Some(sub_dims)},
}

/// The main body of functions
Block: Block = "{" <block_items: (<BlockItem>)*> "}" => Block { <> };

/// Separate Units of Blocks
BlockItem: BlockItem = {
    Decl => BlockItem::Decl(<>),
    Stmt => BlockItem::Stmt(<>),
}

/// Statements
/// Solution of the "dangling else" problem
Stmt: Stmt = {
    MatchedStmt => <>,
    UnMatchedStmt => <>,
}

/// Matched Statements
/// Assign: A statement of assignment.
/// IdleExp: A statement of an optional expression.
/// Block: A block.
/// Return: A statement of return with some value.
MatchedStmt: Stmt = {
    <lval: LVal> "=" <exp: Exp> ";" => Stmt::Assign(Assign { <> }),
    <exp: (Exp)?> ";" => Stmt::IdleExp(IdleExp { <> }),
    Block => Stmt::Block(<>),
    "if" "(" <cond: Exp> ")" <true_stmt: MatchedStmt> "else" <false_stmt: MatchedStmt> => {
        Stmt::IfClause(IfClause{cond: cond, true_stmt: Box::new(true_stmt), false_stmt: Some(Box::new(false_stmt))})
    },
    "while" "(" <cond: Exp> ")" <loop_stmt: MatchedStmt> => {
        Stmt::WhileClause(WhileClause{cond: cond, loop_stmt: Box::new(loop_stmt)})
    },
    "break" ";" => Stmt::Break(Break{}),
    "continue" ";" => Stmt::Continue(Continue{}),
    "return" <exp: Exp> ";" => Stmt::Return(Return { <> }),
}

UnMatchedStmt: Stmt = {
    "if" "(" <cond: Exp> ")" <true_stmt: Stmt> => {
        Stmt::IfClause(IfClause{cond:cond, true_stmt: Box::new(true_stmt), false_stmt: None})
    },
    "if" "(" <cond: Exp> ")" <true_stmt: MatchedStmt> "else" <false_stmt: UnMatchedStmt> => {
        Stmt::IfClause(IfClause{cond:cond, true_stmt: Box::new(true_stmt), false_stmt: Some(Box::new(false_stmt))})
    },
    "while" "(" <cond: Exp> ")" <loop_stmt: UnMatchedStmt> => {
        Stmt::WhileClause(WhileClause{cond: cond, loop_stmt: Box::new(loop_stmt)})
    },
}

/// Expressions. 
Exp: Exp = <lor_exp: LOrExp> => Exp { <> };

/// Left values. Names (identifiers) of variables.
LVal: LVal = <ident: Ident> <idxs: ("[" <Exp> "]")*>=> LVal { <> };

/// Primary expressions. Parenthesized expressions, left values and numbers have the highest priority.
PrimaryExp: PrimaryExp = {
    "(" <Exp> ")" => PrimaryExp::ParenExp(Box::new(<>)),
    LVal => PrimaryExp::LVal(<>),
    Number => PrimaryExp::Number(<>),
}

/// Expressions with unary operator "+", "-" or "!". 
/// Expressions of function calls.
UnaryExp: UnaryExp = {
    PrimaryExp => UnaryExp::PrimaryExp(<>),
    <ident: Ident> "(" ")" => UnaryExp::FuncExp(FuncExp{ident: ident, func_rparams: Vec::new()}),
    <ident: Ident> "(" <func_rparam0: FuncRParam> <mut func_rparams: ("," <FuncRParam>)*> ")" => {
        func_rparams.insert(0, func_rparam0);
        UnaryExp::FuncExp(FuncExp{ident: ident, func_rparams: func_rparams})
    },
    "+" <UnaryExp> => <>,
    <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::UnaryOpExp(unary_op, Box::new(unary_exp)),
}

/// Real Parameters of function calls.
FuncRParam: FuncRParam = <exp: Exp> => FuncRParam{ <> };

/// Unary operators.
UnaryOp: UnaryOp = {
    "-" => UnaryOp::Neg,
    "!" => UnaryOp::Not,
}

/// Expressions with binary operator "*", "/" or "%". 
MulExp: MulExp = {
    UnaryExp => MulExp::UnaryExp(<>),
    <mul_exp: MulExp> <mul_op: MulOp> <unary_exp: UnaryExp> => MulExp::MulUnaryExp(Box::new(mul_exp), mul_op, unary_exp),
}

/// Multiplication-level operators.
MulOp : MulOp = {
    "*" => MulOp::Mul,
    "/" => MulOp::Div,
    "%" => MulOp::Mod,
}

/// Expressions with binary operator "+" or "-".
AddExp: AddExp = {
    MulExp => AddExp::MulExp(<>),
    <add_exp: AddExp> <add_op: AddOp> <mul_exp: MulExp> => {
        AddExp::AddMulExp(Box::new(add_exp), add_op, mul_exp)
    },
}

/// Addition-level operators.
AddOp: AddOp = {
    "+" => AddOp::Add,
    "-" => AddOp::Sub,
}

/// Expressions with comparison operator "<", ">", "<=" or ">=".
RelExp: RelExp = {
    AddExp => RelExp::AddExp(<>),
    <rel_exp: RelExp> <rel_op: RelOp> <add_exp: AddExp> => {
        RelExp::RelAddExp(Box::new(rel_exp), rel_op, add_exp)
    },
}

/// Comparison operators.
RelOp: RelOp = {
    "<" => RelOp::LT,
    ">" => RelOp::GT,
    "<=" => RelOp::LE,
    ">=" => RelOp::GE,
}

/// Expressions with equality comparison operator "==" or "!=".
EqExp: EqExp = {
    RelExp => EqExp::RelExp(<>),
    <eq_exp: EqExp> <eq_op: EqOp> <rel_exp: RelExp> => {
        EqExp::EqRelExp(Box::new(eq_exp), eq_op, rel_exp)
    },
}

/// Equality comparison operators.
EqOp: EqOp = {
    "==" => EqOp::Eq,
    "!=" => EqOp::Neq,
}

/// Expressions with logical operator "&&".
LAndExp: LAndExp = {
    EqExp => LAndExp::EqExp(<>),
    <land_exp: LAndExp> "&&" <eq_exp: EqExp> => {
        LAndExp::LAndEqExp(Box::new(land_exp), eq_exp)
    },
}

/// Expressions with logical operator "||".
LOrExp: LOrExp = {
    LAndExp => LOrExp::LAndExp(<>),
    <lor_exp: LOrExp> "||" <land_exp: LAndExp> => {
        LOrExp::LOrAndExp(Box::new(lor_exp), land_exp)
    },
}

/// Constant expressions
ConstExp: ConstExp = <exp: Exp> => ConstExp{ <> };

/// Terminals
Number: i32 = <num: IntConst> => <>;

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

IntConst: i32 = {
    r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
    r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
    r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}