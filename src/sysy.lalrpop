use crate::ast::*;

grammar;

match {
    // Ignore space and comment.
    r"\s*" => {},
    r"//[^\n\r]*[\n\r]*" => {},
    r"/\*[^*]*\*+(?:[^/*][^*]*\*+)*/" => {},
    // Default
    _
}

pub CompUnit: CompUnit = <func_def: FuncDef> => CompUnit{ <> };

FuncDef: FuncDef = {
    <func_type: FuncType> <ident: Ident> "(" ")" <block: Block> => {
        FuncDef { <> }
    }
}

FuncType: FuncType = "int" => FuncType::Int;

Block: Block = "{" <stmt: Stmt> "}" => Block { <> };

Stmt: Stmt = "return" <exp: Exp> ";" => Stmt::Return(Return { <> }); 

Exp: Exp = <lor_exp: LOrExp> => Exp { <> };

PrimaryExp: PrimaryExp = {
    "(" <Exp> ")" => PrimaryExp::ParenExp(Box::new(<>)),
    Number => PrimaryExp::Number(<>)
}

UnaryExp: UnaryExp = {
    PrimaryExp => UnaryExp::PrimaryExp(<>),
    "+" <UnaryExp> => <>,
    <unary_op: UnaryOp> <unary_exp: UnaryExp> => UnaryExp::UnaryOpExp(unary_op, Box::new(unary_exp))
}

UnaryOp: UnaryOp = {
    "-" => UnaryOp::Neg,
    "!" => UnaryOp::Not,
}

MulExp: MulExp = {
    UnaryExp => MulExp::UnaryExp(<>),
    <mul_exp: MulExp> <mul_op: MulOp> <unary_exp: UnaryExp> => MulExp::MulUnaryExp(Box::new(mul_exp), mul_op, unary_exp),
}

MulOp : MulOp = {
    "*" => MulOp::Mul,
    "/" => MulOp::Div,
    "%" => MulOp::Mod,
}

AddExp: AddExp = {
    MulExp => AddExp::MulExp(<>),
    <add_exp: AddExp> <add_op: AddOp> <mul_exp: MulExp> => {
        AddExp::AddMulExp(Box::new(add_exp), add_op, mul_exp)
    },
}

AddOp: AddOp = {
    "+" => AddOp::Add,
    "-" => AddOp::Sub,
}

RelExp: RelExp = {
    AddExp => RelExp::AddExp(<>),
    <rel_exp: RelExp> <rel_op: RelOp> <add_exp: AddExp> => {
        RelExp::RelAddExp(Box::new(rel_exp), rel_op, add_exp)
    },
}

RelOp: RelOp = {
    "<" => RelOp::LT,
    ">" => RelOp::GT,
    "<=" => RelOp::LE,
    ">=" => RelOp::GE,
}

EqExp: EqExp = {
    RelExp => EqExp::RelExp(<>),
    <eq_exp: EqExp> <eq_op: EqOp> <rel_exp: RelExp> => {
        EqExp::EqRelExp(Box::new(eq_exp), eq_op, rel_exp)
    },
}

EqOp: EqOp = {
    "==" => EqOp::Eq,
    "!=" => EqOp::Neq,
}

LAndExp: LAndExp = {
    EqExp => LAndExp::EqExp(<>),
    <land_exp: LAndExp> "&&" <eq_exp: EqExp> => {
        LAndExp::LAndEqExp(Box::new(land_exp), eq_exp)
    },
}

LOrExp: LOrExp = {
    LAndExp => LOrExp::LAndExp(<>),
    <lor_exp: LOrExp> "||" <land_exp: LAndExp> => {
        LOrExp::LOrAndExp(Box::new(lor_exp), land_exp)
    },
}


Number: i32 = <num: IntConst> => <>;

Ident: String = r"[_a-zA-Z][_a-zA-Z0-9]*" => <>.to_string();

IntConst: i32 = {
    r"[1-9][0-9]*" => i32::from_str_radix(<>, 10).unwrap(),
    r"0[0-7]*" => i32::from_str_radix(<>, 8).unwrap(),
    r"0[xX][0-9a-fA-F]+" => i32::from_str_radix(&<>[2..], 16).unwrap(),
}